plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'com.kezong.fat-aar'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "0.1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

// Usage with GitHub Packages
Properties localProperties = new Properties()
if (rootProject.file("local.properties").exists()) {
    localProperties.load(rootProject.file("local.properties").newDataInputStream())
}
def githubUser = localProperties["GITHUB_USER"] != null ? localProperties["GITHUB_USER"] : System.getenv('GITHUB_USER')
def githubToken = localProperties["GITHUB_TOKEN"] != null ? localProperties["GITHUB_TOKEN"] : System.getenv('GITHUB_TOKEN')
repositories {
    // Usage with Maven Local
//    maven {
//        name = 'AxeptioSDKMavenRepository'
//        url = "file://$projectDir/../maven-repository"
//    }
    // Usage with GithHub Packages
    maven {
        name = 'GitHubPackages'
        url = "https://maven.pkg.github.com/agilitation/axeptio-android-sdk"
        credentials {
            username = githubUser
            password = githubToken
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'

    embed "eu.axeptio:axeptio-sdk-sources:0.1.0"
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                groupId = 'eu.axeptio'
                artifactId = 'axeptio-sdk'
                version = android.defaultConfig.versionName
            }
        }
    }
}